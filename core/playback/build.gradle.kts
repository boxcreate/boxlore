plugins {
    alias(libs.plugins.androidLibrary)
    alias(libs.plugins.dependencyGuard)
    alias(libs.plugins.kover)
}

kover {
    currentProject {
        createVariant("merged") {
            add("debug")
        }
    }
}

android {
    namespace = "cx.aswin.boxlore.core.playback"
    compileSdk = 36

    defaultConfig {
        minSdk = 31
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlin {
        compilerOptions {
            jvmTarget = org.jetbrains.kotlin.gradle.dsl.JvmTarget.JVM_17
        }
    }

    lint {
        disable += "NullSafeMutableLiveData"
        abortOnError = false
        checkReleaseBuilds = true
    }

    testOptions {
        // SmartQueueEngine / QueueSkipMemory touch android.util.Log on the JVM path.
        unitTests.isReturnDefaultValues = true
        unitTests.isIncludeAndroidResources = false
        unitTests.all {
            it.useJUnitPlatform()
        }
    }
}

dependencyGuard {
    configuration("releaseRuntimeClasspath")
}

dependencies {
    api(projects.core.model)
    api(projects.core.network)
    api(projects.core.database)
    api(projects.core.catalog)
    api(projects.core.domain)
    implementation(projects.core.downloads)
    // SmartQueueEngine / MixtapeEngine score via ranking (no longer re-exported by catalog).
    implementation(projects.core.ranking)
    implementation(projects.core.analytics)
    implementation(projects.core.prefs)

    implementation(libs.androidx.core.ktx)
    implementation(libs.kotlinx.coroutines.android)
    implementation(libs.kotlinx.coroutines.guava)

    // Media3 (player + Android Auto session + download service).
    // api: MediaDownloadService extends DownloadService; :app AppContainer references the class.
    api(libs.androidx.media3.exoplayer)
    implementation(libs.androidx.media3.session)
    implementation(libs.androidx.media3.ui)

    // Artwork for session / Auto collages
    implementation(libs.coil)
    implementation(libs.androidx.palette.ktx)

    implementation(libs.gson)
    implementation(libs.okhttp)

    testImplementation(projects.core.testing)
    testImplementation(libs.junit)
    testImplementation(libs.junit.jupiter)
    testRuntimeOnly(libs.junit.vintage.engine)
    testRuntimeOnly(libs.junit.platform.launcher)
    testImplementation(libs.kotlinx.coroutines.test)
    // Robolectric: in-memory Room (QueueRepository) + SharedPreferences/Uri (AutoArtworkRepository).
    testImplementation(libs.robolectric)
    testImplementation("androidx.test:core:1.6.1")
}
